#@TYPE: Machine
#@NAME: NXP i.MX6ULL 14x14 Evaluation Kit
#@SOC: i.MX6ULL
#@DESCRIPTION: Machine configuration for NXP i.MX6ULL 14x14 EVK
#@MAINTAINER: Lauren Post <lauren.post@nxp.com>

MACHINEOVERRIDES =. "mx6:mx6ul:mx6ull:"

include conf/machine/include/imx-base.inc
include conf/machine/include/arm/armv7a/tune-cortexa7.inc

KERNEL_DEVICETREE = " \
    nxp/imx/imx6ull-14x14-evk.dtb \
    nxp/imx/imx6ull-14x14-evk-btwifi.dtb \
    nxp/imx/imx6ull-14x14-evk-emmc.dtb \
    nxp/imx/imx6ull-14x14-evk-gpmi-weim.dtb \
    nxp/imx/imx6ull-14x14-evk-reve.dtb \
    nxp/imx/imx6ull-14x14-evk-reve-btwifi.dtb \
    nxp/imx/imx6ull-14x14-evk-reve-emmc.dtb \
    nxp/imx/imx6ull-14x14-evk-reve-gpmi-weim.dtb \
"

UBOOT_CONFIG ??= "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'sd-optee', 'sd', d)}"
UBOOT_CONFIG[sd] = "mx6ull_14x14_evk_config,sdcard"
UBOOT_CONFIG[emmc] = "mx6ull_14x14_evk_emmc_config,sdcard"
UBOOT_CONFIG[nand] = "mx6ull_14x14_evk_nand_config,ubifs"
UBOOT_CONFIG[qspi1] = "mx6ull_14x14_evk_qspi1_config"
UBOOT_CONFIG[sd-optee] = "mx6ull_14x14_evk_optee_config,sdcard"

OPTEE_BIN_EXT = "6ullevk"

MACHINE_FEATURES += "wifi bluetooth optee bcm4339 bcm43455"
