# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "fsl-bsp-release"
BBFILE_PATTERN_fsl-bsp-release := "^${LAYERDIR}/"
BBFILE_PRIORITY_fsl-bsp-release = "8"
LAYERSERIES_COMPAT_fsl-bsp-release = "kirkstone"

# Define new EULAs and add them to the list defined in meta-freescale.
# See fsl-eula-unpack.bbclass.
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V36 = "b54da624b8d2ffe8a3b47757da48e60f"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V37 = "37cb2d8c2bcf8bf2fbe0366d5d928c29"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V38 = "65c483bb431ccd9a1820e6872c604771"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V39 = "5a0bf11f745e68024f37b4724a5364fe"

FSL_EULA_FILE_MD5SUMS:append = " \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V36} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V37} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V38} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V39} \
"

# Override the EULA defined in meta-freescale.
FSL_EULA_FILE = "${LAYERDIR}/../EULA.txt"
FSL_EULA_FILE_MD5SUM = "${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V39}"

# FIXME: Drop xxd if/when xxd-native can be used instead
HOSTTOOLS_NONFATAL:append = " xxd cert-to-efi-sig-list"

# optee is no longer a valid distro feature. Use machine feature to remove, like this:
# MACHINE_FEATURES:remove = "optee"
# DEPRECATED: The ability to remove optee from the build is deprecated and
# will be removed in some future release.

MACHINE_USES_VIVANTE_KERNEL_DRIVER_MODULE ?= "0"

# Override for use-imx-security-controller-firmware.bbclass
SECO_FIRMWARE_NAME:mx8dxla1-nxp-bsp = "mx8dxla1-ahab-container.img"
SECO_FIRMWARE_NAME:mx8dxlb0-nxp-bsp = "mx8dxlb0-ahab-container.img"

# Overrides for imx-base.inc
IMX_DEFAULT_BSP = "nxp"

# The default bootloader is set to -fslc in
# meta-freescale/cond/machine/include/imx-base.inc, so we must
# override it here.
IMX_DEFAULT_BOOTLOADER:imx-nxp-bsp = "u-boot-imx"

UBOOT_MAKE_TARGET:pn-u-boot-imx:mx6-nxp-bsp = "u-boot.imx"
UBOOT_MAKE_TARGET:pn-u-boot-imx:mx7-nxp-bsp = "u-boot.imx"
UBOOT_SUFFIX:pn-u-boot-imx:mx6-nxp-bsp = "imx"
UBOOT_SUFFIX:pn-u-boot-imx:mx7-nxp-bsp = "imx"
UBOOT_SUFFIX:pn-u-boot-imx:mx8-nxp-bsp = "bin"
UBOOT_MAKE_TARGET:pn-u-boot-imx-mfgtool:mx6-nxp-bsp = "u-boot.imx"
UBOOT_MAKE_TARGET:pn-u-boot-imx-mfgtool:mx7-nxp-bsp = "u-boot.imx"
UBOOT_SUFFIX:pn-u-boot-imx-mfgtool:mx6-nxp-bsp = "imx"
UBOOT_SUFFIX:pn-u-boot-imx-mfgtool:mx7-nxp-bsp = "imx"
UBOOT_SUFFIX:pn-u-boot-imx-mfgtool:mx8-nxp-bsp = "bin"

IMX_DEFAULT_UBOOTTOOLS = "u-boot-imx-tools"
PREFERRED_PROVIDER_u-boot-tools-native = "${IMX_DEFAULT_UBOOTTOOLS}-native"
PREFERRED_PROVIDER_nativesdk-u-boot-tools = "nativesdk-${IMX_DEFAULT_UBOOTTOOLS}"
PREFERRED_PROVIDER_u-boot-mkimage-native = "${IMX_DEFAULT_UBOOTTOOLS}-native"
PREFERRED_PROVIDER_nativesdk-u-boot-mkimage = "nativesdk-${IMX_DEFAULT_UBOOTTOOLS}"

# Avoid multiple runtime providers for u-boot-default-env
PREFERRED_RPROVIDER_u-boot-default-env ??= "${IMX_DEFAULT_BOOTLOADER}"

MACHINE_SOCARCH_FILTER:remove = "alsa-lib gstreamer1.0 imx-codec"

# Use latest SDMA firmware from firmware-imx instead of upstream linux-firmware
MACHINE_FIRMWARE:remove:mx6-nxp-bsp      = "linux-firmware-imx-sdma-imx6q"
MACHINE_FIRMWARE:remove:mx7d-nxp-bsp     = "linux-firmware-imx-sdma-imx7d"
MACHINE_FIRMWARE:remove:mx8-nxp-bsp      = "linux-firmware-imx-sdma-imx7d"
MACHINE_FIRMWARE:append:mx6-nxp-bsp      = " firmware-imx-sdma-imx6q"
MACHINE_FIRMWARE:append:mx7-nxp-bsp      = " firmware-imx-sdma-imx7d"
# imx6ul7d doesn't have mx7 in MACHINEOVERRIDES but needs both imx6q and imx7d firmware

MACHINE_FIRMWARE:append:imx6ul7d = " firmware-imx-sdma-imx7d"
MACHINE_FIRMWARE:append:mx8-nxp-bsp      = " firmware-imx-sdma-imx7d"

MACHINE_FIRMWARE:append:mx6ulz-nxp-bsp = " firmware-imx-epdc"
MACHINE_FIRMWARE:append:mx8-nxp-bsp    = " linux-firmware-ath10k"
MACHINE_FIRMWARE:append:mx8qm-nxp-bsp  = " firmware-imx-vpu-imx8 firmware-imx-hdmi firmware-imx-xuvi-imx8 sof-imx sof-zephyr zephyr-demo-imx"
MACHINE_FIRMWARE:remove:mx8qxp-nxp-bsp = "firmware-imx-vpu-imx8qxp"
MACHINE_FIRMWARE:append:mx8qxp-nxp-bsp = " firmware-imx-vpu-imx8 sof-imx sof-zephyr zephyr-demo-imx"
MACHINE_FIRMWARE:remove:mx8dx-nxp-bsp  = "firmware-imx-vpu-imx8qxp"
MACHINE_FIRMWARE:append:mx8dx-nxp-bsp  = " firmware-imx-vpu-imx8 sof-imx sof-zephyr"
MACHINE_FIRMWARE:append:mx8mn-nxp-bsp  = " firmware-imx-easrc-imx8mn"
MACHINE_FIRMWARE:append:mx8mnul-nxp-bsp = " firmware-imx-easrc-imx8mn"
MACHINE_FIRMWARE:remove:mx8mp-nxp-bsp  = "firmware-sof-imx"
MACHINE_FIRMWARE:append:mx8mp-nxp-bsp  = " sof-imx sof-zephyr zephyr-demo-imx"
MACHINE_FIRMWARE:remove:mx8mpul-nxp-bsp  = "firmware-sof-imx"
MACHINE_FIRMWARE:append:mx8mpul-nxp-bsp  = " sof-imx sof-zephyr"
MACHINE_FIRMWARE:append:mx8ulp-nxp-bsp = " sof-imx sof-zephyr"

# NXP WiFi firmware & extra Wlan SDK
MACHINE_FIRMWARE:append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8987', 'linux-firmware-nxp89xx', '', d)}"
MACHINE_FIRMWARE:append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8997', 'linux-firmware-nxp89xx', '', d)}"
MACHINE_FIRMWARE:append = " ${@bb.utils.contains('MACHINE_FEATURES', 'iw612', 'linux-firmware-nxp89xx', '', d)}"
MACHINE_EXTRA_RRECOMMENDS:append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8987', 'nxp-wlan-sdk kernel-module-nxp89xx', '', d)}"
MACHINE_EXTRA_RRECOMMENDS:append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8997', 'nxp-wlan-sdk kernel-module-nxp89xx', '', d)}"
MACHINE_EXTRA_RRECOMMENDS:append = " ${@bb.utils.contains('MACHINE_FEATURES', 'iw612', 'iw612-sdk kernel-module-iw612', '', d)}"

# MCore Demo apps to /lib/firmware
MACHINE_EXTRA_RRECOMMENDS:append:mx7ulp-nxp-bsp  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8mm-nxp-bsp   = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8mn-nxp-bsp   = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8mnul-nxp-bsp = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8mp-nxp-bsp   = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8mpul-nxp-bsp = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8mq-nxp-bsp   = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8qm-nxp-bsp   = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8qxp-nxp-bsp  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8dx-nxp-bsp   = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx8dxl-nxp-bsp  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS:append:mx93-nxp-bsp    = " imx-m33-demos"

MACHINE_GSTREAMER_1_0_PLUGIN:mx6dl-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6q-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6sl-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6sll-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6sx-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6ul-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6ull-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx6ulz-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx7d-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx7ulp-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx8-nxp-bsp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN:mx9-nxp-bsp ?= "imx-gst1.0-plugin"

IMX_DEFAULT_KERNEL:mx6ulz-nxp-bsp = "linux-imx"

OPTEE_BOOT_IMAGE:mx6-nxp-bsp ?= "tee.bin uTee-${OPTEE_BIN_EXT}"
OPTEE_BOOT_IMAGE:mx7-nxp-bsp ?= "tee.bin uTee-${OPTEE_BIN_EXT}"
OPTEE_BOOT_IMAGE:mx8-nxp-bsp ?= "tee.bin"

IMAGE_INSTALL:append = " \
    ${@bb.utils.contains('COMBINED_FEATURES', 'jailhouse', 'jailhouse', '', d)} \
    ${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'packagegroup-fsl-optee-imx', '', d)} \
"

# To add a new array override for a specific upstream machine, set the
# array value with a variable, then set the default variable value to
# UNSUPPORTED and the machine-specific variable value as required.
# For example:
#   UBOOT_CONFIG[fspi] = "${UBOOT_CONFIG_FSPI}"
#   UBOOT_CONFIG_FSPI                   = "UNSUPPORTED"
#   UBOOT_CONFIG_FSPI:imx8dxl-ddr3l-evk = "${UBOOT_CONFIG_BASENAME}_fspi_defconfig"

MACHINE_FEATURES_IMX_REMOVALS ?= "mrvl8997"
MACHINE_FEATURES:remove        = "${MACHINE_FEATURES_IMX_REMOVALS}"
MACHINE_FEATURES:append:mx6-nxp-bsp    = " nxp8987"
MACHINE_FEATURES:append:mx7-nxp-bsp    = " nxp8987"
MACHINE_FEATURES:append:mx8-nxp-bsp    = " nxp8987"
MACHINE_FEATURES:append:mx8m-nxp-bsp    = " nxp8987"

# Overrides for imx6slevk.conf
MACHINE_FEATURES:append:imx6slevk = " optee"

# Overrides for imx6sllevk.conf
MACHINE_FEATURES:append:imx6sllevk = " optee"
KERNEL_DEVICETREE:append:imx6sllevk = " \
    imx6sll-evk-btwifi.dtb \
    imx6sll-evk-reva.dtb \
"

# Overrides for imx6sxsabresd.conf
MACHINE_FEATURES:append:imx6sxsabresd = " optee"

# Overrides for imx6ulevk.conf
MACHINE_FEATURES:append:imx6ulevk = " optee"
KERNEL_DEVICETREE:remove:imx6ulevk = " \
    imx6ul-14x14-evk-btwifi-oob.dtb \
"
KERNEL_DEVICETREE:append:imx6ulevk = " \
    imx6ul-14x14-evk-btwifi-sdio3_0.dtb \
"

# Overrides for imx6ullevk.conf
MACHINE_FEATURES:append:imx6ullevk = " optee"
KERNEL_DEVICETREE:remove:imx6ullevk = " \
    imx6ull-14x14-evk-btwifi-oob.dtb \
"
KERNEL_DEVICETREE:append:imx6ullevk = " \
    imx6ull-14x14-evk-btwifi-sdio3_0.dtb \
"

# Overrides for imx6ulz-14x14-evk.conf
MACHINE_FEATURES:append:imx6ulz-14x14-evk = " optee"
KERNEL_DEVICETREE:append:imx6ulz-14x14-evk = " \
    imx6ulz-14x14-evk-btwifi-sdio3_0.dtb \
"

# Overrides for imx7dsabresd.conf
MACHINE_FEATURES:append:imx7dsabresd = " optee"
WKS_FILE_DEPENDS:append:imx7dsabresd = " imx-m4-demos"
IMAGE_BOOT_FILES:append:imx7dsabresd = " \
    imx7d_sabresd_m4_TCM_Pingpang.bin \
    imx7d_sabresd_m4_TCM_helloworld.bin \
    imx7d_sabresd_m4_TCM_mcctty.bin \
"

KERNEL_DEVICETREE:append:imx7dsabresd = " \
    imx7d-sdb-mqs.dtb \
"

# Overrides for imx7ulpevk.conf
MACHINE_FEATURES:append:imx7ulpevk = " optee"
WKS_FILE_DEPENDS:append:imx7ulpevk = " imx-m4-demos"
IMAGE_BOOT_FILES:append:imx7ulpevk = " \
    imx7ulp_m4_demo.img \
    imx7ulp_wireless_uart_bridge.img \
    imx7ulp_erpc_matrix_multiply_rpmsg_rtos_imxcm4.img \
    imx7ulp_rpmsg_lite_pingpong_rtos.img \
    imx7ulp_rpmsg_lite_str_echo_rtos.img \
"

# Overrides for imx8mm-lpddr4-evk.conf
MACHINE_FEATURES:append:imx8mm-lpddr4-evk = " dpdk"
WKS_FILE_DEPENDS:append:imx8mm-lpddr4-evk = " imx-m4-demos"
IMAGE_BOOT_FILES:append:imx8mm-lpddr4-evk = " \
    imx8mm_m4_TCM_hello_world.bin \
    imx8mm_m4_TCM_low_power_voice.bin \
    imx8mm_m4_TCM_low_power_wakeword.bin \
    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mm_m4_TCM_sai_low_power_audio.bin \
    imx8mm_m4_TCM_sai_low_power_audio_wm8524.bin \
"
KERNEL_DEVICETREE:append:imx8mm-lpddr4-evk = " \
    freescale/imx8mm-evk-dpdk.dtb \
    freescale/imx8mm-evk-ecspi-slave.dtb \
    freescale/imx8mm-evk-lk.dtb \
    freescale/imx8mm-evk-rm67191-cmd-ram.dtb \
    freescale/imx8mm-evk-rm67199.dtb \
    freescale/imx8mm-evk-rm67199-cmd-ram.dtb \
    freescale/imx8mm-evk-rpmsg-wm8524.dtb \
    freescale/imx8mm-evk-rpmsg-wm8524-lpv.dtb \
"

# Overrides for imx8mm-ddr4-evk.conf
MACHINE_FEATURES:append:imx8mm-ddr4-evk = " dpdk"
WKS_FILE_DEPENDS:append:imx8mm-ddr4-evk = " imx-m4-demos"
IMAGE_BOOT_FILES:append:imx8mm-ddr4-evk = " \
    imx8mm_m4_TCM_hello_world.bin \
    imx8mm_m4_TCM_low_power_voice.bin \
    imx8mm_m4_TCM_low_power_wakeword.bin \
    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mm_m4_TCM_sai_low_power_audio.bin \
    imx8mm_m4_TCM_sai_low_power_audio_wm8524.bin \
"
KERNEL_DEVICETREE:append:imx8mm-ddr4-evk = " \
    freescale/imx8mm-ddr4-evk-revb-rm67199.dtb \
    freescale/imx8mm-ddr4-evk-revb-rm67191-cmd-ram.dtb \
    freescale/imx8mm-ddr4-evk-revb-rm67199-cmd-ram.dtb \
    freescale/imx8mm-ddr4-evk-rm67199.dtb \
    freescale/imx8mm-ddr4-evk-rm67191-cmd-ram.dtb \
    freescale/imx8mm-ddr4-evk-rm67199-cmd-ram.dtb \
"
KERNEL_DEVICETREE:remove:imx8mm-ddr4-evk = " \
    freescale/imx8mm-ddr4-evk-inmate.dtb \
    freescale/imx8mm-ddr4-evk-root.dtb \
"

# Overrides for imx8mn-lpddr4-evk.conf
#MACHINE_FEATURES_IMX_REMOVALS:append:imx8mn-lpddr4-evk = " jailhouse"
WKS_FILE_DEPENDS:append:imx8mn-lpddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES:append:imx8mn-lpddr4-evk = " \
    imx8mn_m7_TCM_hello_world.bin \
    imx8mn_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mn_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mn_m7_TCM_sai_low_power_audio.bin \
"
KERNEL_DEVICETREE:append:imx8mn-lpddr4-evk = " \
    freescale/imx8mn-evk-8mic-swpdm.dtb \
    freescale/imx8mn-evk-lk.dtb \
    freescale/imx8mn-evk-rm67191-cmd-ram.dtb \
    freescale/imx8mn-evk-rm67199.dtb \
    freescale/imx8mn-evk-rm67199-cmd-ram.dtb \
    freescale/imx8mn-evk-usd-wifi.dtb \
"

# Overrides for imx8mn-ddr4-evk.conf
#MACHINE_FEATURES_IMX_REMOVALS:append:imx8mn-ddr4-evk = " jailhouse"
WKS_FILE_DEPENDS:append:imx8mn-ddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES:append:imx8mn-ddr4-evk = " \
    imx8mn_m7_TCM_hello_world.bin \
    imx8mn_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mn_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mn_m7_TCM_sai_low_power_audio.bin \
"
KERNEL_DEVICETREE:append:imx8mn-ddr4-evk = " \
    freescale/imx8mn-ddr4-evk-lk.dtb \
    freescale/imx8mn-ddr4-evk-rm67199.dtb \
    freescale/imx8mn-ddr4-evk-rm67191-cmd-ram.dtb \
    freescale/imx8mn-ddr4-evk-rm67199-cmd-ram.dtb \
"
KERNEL_DEVICETREE:remove:imx8mn-ddr4-evk = " \
    freescale/imx8mn-ddr4-evk-inmate.dtb \
    freescale/imx8mn-ddr4-evk-root.dtb \
"

# Overrides for imx8mp-lpddr4-evk
#MACHINE_FEATURES_IMX_REMOVALS:append:imx8mp-lpddr4-evk = " jailhouse"
MACHINE_FEATURES:append:imx8mp-lpddr4-evk = " optee nxp8997 dpdk"
KERNEL_DEVICETREE:remove:imx8mp-lpddr4-evk = " \
    freescale/imx8mp-evk-dsp-lpa.dtb \
"
KERNEL_DEVICETREE:append:imx8mp-lpddr4-evk = " \
    freescale/imx8mp-evk-dpdk.dtb \
    freescale/imx8mp-evk-dsp.dtb \
    freescale/imx8mp-evk-dual-os08a20.dtb \
    freescale/imx8mp-evk-ecspi-slave.dtb \
    freescale/imx8mp-evk-ndm.dtb \
    freescale/imx8mp-evk-os08a20.dtb \
    freescale/imx8mp-evk-os08a20-ov5640.dtb \
    freescale/imx8mp-evk-revA3-8mic-revE.dtb \
    freescale/imx8mp-evk-rm67199.dtb \
    freescale/imx8mp-evk-rpmsg-lpv.dtb \
    freescale/imx8mp-evk-usdhc1-m2.dtb \
    freescale/imx8mp-evk-8mic-swpdm.dtb \
"
WKS_FILE_DEPENDS:append:imx8mp-lpddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES:append:imx8mp-lpddr4-evk = " \
    imx8mp_m7_TCM_hello_world.bin \
    imx8mp_m7_TCM_low_power_voice.bin \
    imx8mp_m7_TCM_low_power_wakeword.bin \
    imx8mp_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mp_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mp_m7_TCM_sai_low_power_audio.bin \
"

# Overrides for imx8mp-ddr4-evk
#MACHINE_FEATURES_IMX_REMOVALS:append:imx8mp-ddr4-evk = " jailhouse"
MACHINE_FEATURES:append:imx8mp-ddr4-evk = " optee nxp8997 dpdk"
WKS_FILE_DEPENDS:append:imx8mp-ddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES:append:imx8mp-ddr4-evk = " \
    imx8mp_m7_TCM_hello_world.bin \
    imx8mp_m7_TCM_low_power_voice.bin \
    imx8mp_m7_TCM_low_power_wakeword.bin \
    imx8mp_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mp_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mp_m7_TCM_sai_low_power_audio.bin \
"
IMXBOOT_TARGETS:append:mx8m-nxp-bsp = \
    " ${@bb.utils.contains('MACHINE_FEATURES', 'optee stmm', 'flash_evk_stmm_capsule', \
                                                       '', d)} "
# Overrides for imx8mq-evk.conf
MACHINE_FEATURES:append:imx8mq-evk = " jailhouse nxp8997"
WKS_FILE_DEPENDS:append:imx8mq-evk = " imx-m4-demos"
IMAGE_BOOT_FILES:append:imx8mq-evk = " \
    imx8mq_m4_TCM_hello_world.bin \
    imx8mq_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mq_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
"

# Overrides for imx8qm-mek.conf
MACHINE_FEATURES:append:imx8qm-mek = " nxp8997"
KERNEL_DEVICETREE:remove:imx8qm-mek = " \
    freescale/imx8qm-mek-dom0.dtb \
    freescale/imx8qm-mek-domu.dtb \
    freescale/imx8qm-mek-dsp.dtb \
    freescale/imx8qm-mek-inmate.dtb \
    freescale/imx8qm-mek-root.dtb \
"
KERNEL_DEVICETREE:append:imx8qm-mek = " \
    freescale/imx8qm-mek-hdmi-rx.dtb \
    freescale/imx8qm-mek-hdmi-rx-ov5640.dtb \
    freescale/imx8qm-mek-sof.dtb \
"
RDEPENDS:${KERNEL_PACKAGE_NAME}-image:imx8qm-mek = ""
IMXBOOT_TARGETS_SD:prepend:imx8qm-mek = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8qxp-mek.conf
MACHINE_FEATURES:append:imx8qxp-mek = " nxp8997"
KERNEL_DEVICETREE:remove:imx8qxp-mek = " \
    freescale/imx8qxp-mek-dsp.dtb \
    freescale/imx8qxp-mek-dom0.dtb \
    freescale/imx8qxp-mek-inmate.dtb \
    freescale/imx8qxp-mek-root.dtb \
"
KERNEL_DEVICETREE:append:imx8qxp-mek = " \
    freescale/imx8qxp-mek-sof.dtb \
"
RDEPENDS:${KERNEL_PACKAGE_NAME}-image:imx8qxp-mek = ""
IMXBOOT_TARGETS_SD:prepend:imx8qxp-mek = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8dxl-lpddr4-evk.conf
MACHINE_FEATURES:append:imx8dxl-lpddr4-evk = " nxp8997"
KERNEL_DEVICETREE:remove:imx8dxl-lpddr4-evk = " \
    freescale/imx8dxl-evk-inmate.dtb \
    freescale/imx8dxl-evk-root.dtb \
"
RDEPENDS:${KERNEL_PACKAGE_NAME}-image:imx8dxl-lpddr4-evk = ""
IMXBOOT_TARGETS_SD:prepend:imx8dxl-lpddr4-evk = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4 flash_linux_m4_no_v2x ', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8dxl-ddr3l-evk.conf
MACHINE_FEATURES:append:imx8dxl-ddr3l-evk = " nxp8997"
RDEPENDS:${KERNEL_PACKAGE_NAME}-image:imx8dxl-ddr3l-evk = ""
KERNEL_DEVICETREE_BASENAME:imx8dxl-ddr3l-evk = "imx8dxl-ddr3l-evk"
UBOOT_CONFIG_BASENAME:imx8dxl-ddr3l-evk = "imx8dxl_ddr3l_evk"
IMXBOOT_TARGETS_SD:prepend:imx8dxl-ddr3l-evk = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4 flash_linux_m4_no_v2x', \
                                                       'flash_regression_linux_m4', d)} "

# Create a mirror for unreliable packages
MIRRORS += "http://sourceforge.net/.* http://www.nxp.com/lgfiles/updates/other"
