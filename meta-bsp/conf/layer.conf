# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "fsl-bsp-release"
BBFILE_PATTERN_fsl-bsp-release := "^${LAYERDIR}/"
BBFILE_PRIORITY_fsl-bsp-release = "8"
LAYERSERIES_COMPAT_fsl-bsp-release = "gatesgarth hardknott"

# Define new EULAs and add them to the list defined in meta-freescale.
# See fsl-eula-unpack.bbclass.
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V18 = "231e11849a4331fcbb19d7f4aab4a659"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V19 = "a632fefd1c359980434f9389833cab3a"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V22 = "3c3fe2b904fd694f28d2f646ee16dddb"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V23 = "85d06b38f17b935ca11784d67075b846"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V24 = "417b82f17fc02b88125331ed312f6f1b"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V24.1 = "b3b0bab6ff55420d043cf1652c52d788"
FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V25 = "f35df765ff17e69043ea21f350e3229c"

FSL_EULA_FILE_MD5SUMS_append = " \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V18} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V19} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V22} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V23} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V24} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V24.1} \
    ${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V25} \
"

# Override the EULA defined in meta-freescale.
FSL_EULA_FILE = "${LAYERDIR}/../EULA.txt"
FSL_EULA_FILE_MD5SUM = "${FSL_EULA_FILE_MD5SUM_LA_OPT_NXP_SOFTWARE_LICENSE_V24.1}"

HOSTTOOLS_NONFATAL_append = " bc rsync xxd"

# optee is no longer a valid distro feature. Use machine feature to remove, like this:
# MACHINE_FEATURES_remove = "optee"
# DEPRECATED: The ability to remove optee from the build is deprecated and
# will be removed in some future release.

DISTRO_FEATURES_FILTER_NATIVESDK += "wayland"

MACHINE_USES_VIVANTE_KERNEL_DRIVER_MODULE ?= "0"

# Overrides for imx-base.inc
IMX_DEFAULT_BSP = "nxp"

# The default bootloader is set to -fslc in
# meta-freescale/cond/machine/include/imx-base.inc, so we must
# override it here.
IMX_DEFAULT_BOOTLOADER_imx = "u-boot-imx"

UBOOT_MAKE_TARGET_pn-u-boot-imx_mx6 = "u-boot.imx"
UBOOT_MAKE_TARGET_pn-u-boot-imx_mx7 = "u-boot.imx"
UBOOT_SUFFIX_pn-u-boot-imx_mx6 = "imx"
UBOOT_SUFFIX_pn-u-boot-imx_mx7 = "imx"
UBOOT_SUFFIX_pn-u-boot-imx_mx8 = "bin"
UBOOT_MAKE_TARGET_pn-u-boot-imx-mfgtool_mx6 = "u-boot.imx"
UBOOT_MAKE_TARGET_pn-u-boot-imx-mfgtool_mx7 = "u-boot.imx"
UBOOT_SUFFIX_pn-u-boot-imx-mfgtool_mx6 = "imx"
UBOOT_SUFFIX_pn-u-boot-imx-mfgtool_mx7 = "imx"
UBOOT_SUFFIX_pn-u-boot-imx-mfgtool_mx8 = "bin"

IMX_DEFAULT_UBOOTTOOLS = "u-boot-imx-tools"
PREFERRED_PROVIDER_u-boot-tools-native = "${IMX_DEFAULT_UBOOTTOOLS}-native"
PREFERRED_PROVIDER_nativesdk-u-boot-tools = "nativesdk-${IMX_DEFAULT_UBOOTTOOLS}"
PREFERRED_PROVIDER_u-boot-mkimage-native = "${IMX_DEFAULT_UBOOTTOOLS}-native"
PREFERRED_PROVIDER_nativesdk-u-boot-mkimage = "nativesdk-${IMX_DEFAULT_UBOOTTOOLS}"

# Avoid multiple runtime providers for u-boot-default-env
PREFERRED_RPROVIDER_u-boot-default-env ??= "${IMX_DEFAULT_BOOTLOADER}"

MACHINEOVERRIDES_EXTENDER_mx8mnul       = "imxfbdev"
MACHINEOVERRIDES_EXTENDER_mx8phantomdxl = "imxfbdev"

MACHINE_SOCARCH_SUFFIX_mx8phantomdxl = "-mx8dxl"
MACHINE_SOCARCH_SUFFIX_mx8mnul       = "-mx8mnul"

MACHINE_SOCARCH_FILTER_remove = "alsa-lib gstreamer1.0 imx-codec"

# Use latest SDMA firmware from firmware-imx instead of upstream linux-firmware
MACHINE_FIRMWARE_remove_mx6  = "linux-firmware-imx-sdma-imx6q"
MACHINE_FIRMWARE_remove_mx7d = "linux-firmware-imx-sdma-imx7d"
MACHINE_FIRMWARE_remove_mx8 = "linux-firmware-imx-sdma-imx7d"
MACHINE_FIRMWARE_append_mx6  = " firmware-imx-sdma firmware-imx-regulatory"
MACHINE_FIRMWARE_append_mx7  = " firmware-imx-sdma firmware-imx-regulatory"

MACHINE_FIRMWARE_append_mx6ulz = " firmware-imx-epdc"
MACHINE_FIRMWARE_append_mx8    = " linux-firmware-ath10k firmware-imx-sdma firmware-imx-regulatory"
MACHINE_FIRMWARE_append_mx8qm  = " firmware-imx-vpu-imx8 firmware-imx-hdmi firmware-imx-xuvi sof-imx"
MACHINE_FIRMWARE_remove_mx8qxp = "firmware-imx-vpu-imx8qxp"
MACHINE_FIRMWARE_append_mx8qxp = " firmware-imx-vpu-imx8 sof-imx"
MACHINE_FIRMWARE_append_mx8mn  = " firmware-imx-easrc"
MACHINE_FIRMWARE_remove_mx8mp  = "firmware-imx-easrc-imx8mn firmware-imx-xcvr-imx8mp firmware-sof-imx"
MACHINE_FIRMWARE_append_mx8mnul = " firmware-imx-easrc"
MACHINE_FIRMWARE_append_mx8mp  = " firmware-imx-easrc firmware-imx-xcvr sof-imx"

# NXP WiFi firmware & extra Wlan SDK
MACHINE_FIRMWARE_append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8987', 'linux-firmware-nxp89xx', '', d)}"
MACHINE_FIRMWARE_append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8997', 'linux-firmware-nxp89xx', '', d)}"
MACHINE_EXTRA_RRECOMMENDS_append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8987', 'nxp-wlan-sdk kernel-module-nxp89xx', '', d)}"
MACHINE_EXTRA_RRECOMMENDS_append = " ${@bb.utils.contains('MACHINE_FEATURES', 'nxp8997', 'nxp-wlan-sdk kernel-module-nxp89xx', '', d)}"

# MCore Demo apps to /lib/firmware
MACHINE_EXTRA_RRECOMMENDS_append_mx7ulp = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8dxl  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8mm  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8mn  = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8mnul = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8mp  = " imx-m7-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8mq  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8qm  = " imx-m4-demos"
MACHINE_EXTRA_RRECOMMENDS_append_mx8qxp  = " imx-m4-demos"

MACHINE_GSTREAMER_1_0_PLUGIN_mx6dl ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6q ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6sl ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6sll ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6sx ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6ul ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6ull ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx6ulz ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx7d ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx7ulp ?= "imx-gst1.0-plugin"
MACHINE_GSTREAMER_1_0_PLUGIN_mx8 ?= "imx-gst1.0-plugin"

PREFERRED_VERSION_weston_mx6 ?= "9.0.0.imx"
PREFERRED_VERSION_weston_mx7 ?= "9.0.0.imx"
PREFERRED_VERSION_weston_mx8 ?= "9.0.0.imx"

PREFERRED_VERSION_wayland-protocols_mx6 = "1.20.imx"
PREFERRED_VERSION_wayland-protocols_mx7 = "1.20.imx"
PREFERRED_VERSION_wayland-protocols_mx8 = "1.20.imx"

PREFERRED_VERSION_libdrm_mx6 ?= "2.4.102.imx"
PREFERRED_VERSION_libdrm_mx7 ?= "2.4.102.imx"
PREFERRED_VERSION_libdrm_mx8 ?= "2.4.102.imx"

PREFERRED_VERSION_opencv_mx6 ?= "4.5.2.imx"
PREFERRED_VERSION_opencv_mx7 ?= "4.5.2.imx"
PREFERRED_VERSION_opencv_mx8 ?= "4.5.2.imx"

PREFERRED_VERSION_isp-imx ?= "4.2.2.13.0"
PREFERRED_VERSION_basler-camera ?= "4.2.2.13.0"

PREFERRED_VERSION_optee-client_mx6 = "3.13.0.imx"
PREFERRED_VERSION_optee-client_mx7 = "3.13.0.imx"
PREFERRED_VERSION_optee-client_mx8 = "3.13.0.imx"
PREFERRED_VERSION_optee-os_mx6 = "3.13.0.imx"
PREFERRED_VERSION_optee-os_mx7 = "3.13.0.imx"
PREFERRED_VERSION_optee-os_mx8 = "3.13.0.imx"
PREFERRED_VERSION_optee-test_mx6 = "3.13.0.imx"
PREFERRED_VERSION_optee-test_mx7 = "3.13.0.imx"
PREFERRED_VERSION_optee-test_mx8 = "3.13.0.imx"

IMX_DEFAULT_KERNEL_mx6ulz = "linux-imx"

SOC_DEFAULT_IMAGE_FSTYPES_IMX_REMOVALS ?= "wic.gz"
SOC_DEFAULT_IMAGE_FSTYPES_remove = "${SOC_DEFAULT_IMAGE_FSTYPES_IMX_REMOVALS}"
SOC_DEFAULT_IMAGE_FSTYPES_append = " wic.bz2 tar.bz2"

OPTEE_BOOT_IMAGE_mx6 ?= "tee.bin uTee-${OPTEE_BIN_EXT}"
OPTEE_BOOT_IMAGE_mx7 ?= "tee.bin uTee-${OPTEE_BIN_EXT}"
OPTEE_BOOT_IMAGE_mx8 ?= "tee.bin"

IMAGE_BOOT_FILES_append = " \
    ${@bb.utils.contains('COMBINED_FEATURES', 'xen', 'xen', '', d)} \
"

IMAGE_INSTALL_append = " \
    ${@bb.utils.contains('COMBINED_FEATURES', 'jailhouse', 'jailhouse', '', d)} \
    ${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'packagegroup-fsl-optee-imx', '', d)} \
    ${@bb.utils.contains('COMBINED_FEATURES', 'xen', 'imx-xen xen-tools', '', d)} \
"

# To add a new array override for a specific machine, like for UBOOT_CONFIG,
# set the array value with a variable, then set the variable to UNSUPPORTED
# by default, then override the variable for the specific machine. For example:
# UBOOT_CONFIG_NOM                 = "UNSUPPORTED"
# UBOOT_CONFIG_NOM_imx8mn-ddr4-evk = "imx8mn_ddr4_evk_nom_defconfig"
# UBOOT_CONFIG[nom] = "${UBOOT_CONFIG_NOM}"

MACHINE_FEATURES_IMX_REMOVALS ?= "qca9377 mrvl8997"
MACHINE_FEATURES_remove        = "${MACHINE_FEATURES_IMX_REMOVALS}"
MACHINE_FEATURES_append_imx    = " nxp8987"

# Overrides for imx6slevk.conf
MACHINE_FEATURES_append_imx6slevk = " optee"

# Overrides for imx6sllevk.conf
MACHINE_FEATURES_append_imx6sllevk = " optee"
KERNEL_DEVICETREE_append_imx6sllevk = " \
    imx6sll-evk-btwifi.dtb \
    imx6sll-evk-reva.dtb \
"

# Overrides for imx6sxsabreauto.conf
MACHINE_FEATURES_append_imx6sxsabreauto = " optee"

# Overrides for imx6sxsabresd.conf
MACHINE_FEATURES_append_imx6sxsabresd = " optee"

# Overrides for imx6ulevk.conf
MACHINE_FEATURES_append_imx6ulevk = " optee"

# Overrides for imx6ulz-14x14-evk.conf
MACHINE_FEATURES_append_imx6ulz-14x14-evk = " optee"

# Overrides for imx7dsabresd.conf
MACHINE_FEATURES_append_imx7dsabresd = " optee"
WKS_FILE_DEPENDS_append_imx7dsabresd = " imx-m4-demos"
IMAGE_BOOT_FILES_append_imx7dsabresd = " \
    imx7d_sabresd_m4_TCM_Pingpang.bin \
    imx7d_sabresd_m4_TCM_helloworld.bin \
    imx7d_sabresd_m4_TCM_mcctty.bin \
"

# Overrides for imx7ulpevk.conf
MACHINE_FEATURES_append_imx7ulpevk = " optee"
WKS_FILE_DEPENDS_append_imx7ulpevk = " imx-m4-demos"
IMAGE_BOOT_FILES_append_imx7ulpevk = " \
    imx7ulp_m4_demo.img \
    imx7ulp_wireless_uart_bridge.img \
    imx7ulp_erpc_matrix_multiply_rpmsg_rtos_imxcm4.img \
    imx7ulp_rpmsg_lite_pingpong_rtos.img \
    imx7ulp_rpmsg_lite_str_echo_rtos.img \
"

# Overrides for imx8mm-lpddr4-evk.conf
WKS_FILE_DEPENDS_append_imx8mm-lpddr4-evk = " imx-m4-demos"
IMAGE_BOOT_FILES_append_imx8mm-lpddr4-evk = " \
    imx8mm_m4_TCM_hello_world.bin \
    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mm_m4_TCM_sai_low_power_audio.bin \
"
KERNEL_DEVICETREE_append_imx8mm-lpddr4-evk = " \
    freescale/imx8mm-evk-ecspi-slave.dtb \
"

# Overrides for imx8mm-ddr4-evk.conf
WKS_FILE_DEPENDS_append_imx8mm-ddr4-evk = " imx-m4-demos"
IMAGE_BOOT_FILES_append_imx8mm-ddr4-evk = " \
    imx8mm_m4_TCM_hello_world.bin \
    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mm_m4_TCM_sai_low_power_audio.bin \
"

# Overrides for imx8mn-lpddr4-evk.conf
#MACHINE_FEATURES_IMX_REMOVALS_append_imx8mn-lpddr4-evk = " jailhouse"
WKS_FILE_DEPENDS_append_imx8mn-lpddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES_append_imx8mn-lpddr4-evk = " \
    imx8mn_m7_TCM_hello_world.bin \
    imx8mn_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mn_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mn_m7_TCM_sai_low_power_audio.bin \
"

# Overrides for imx8mn-ddr4-evk.conf
#MACHINE_FEATURES_IMX_REMOVALS_append_imx8mn-ddr4-evk = " jailhouse"
WKS_FILE_DEPENDS_append_imx8mn-ddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES_append_imx8mn-ddr4-evk = " \
    imx8mn_m7_TCM_hello_world.bin \
    imx8mn_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mn_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mn_m7_TCM_sai_low_power_audio.bin \
"
UBOOT_CONFIG_NOM                 = "UNSUPPORTED"
UBOOT_CONFIG_NOM_imx8mn-ddr4-evk = "imx8mn_ddr4_evk_nom_defconfig"
UBOOT_CONFIG[nom] = "${UBOOT_CONFIG_NOM}"

# Overrides for imx8mp-lpddr4-evk
#MACHINE_FEATURES_IMX_REMOVALS_append_imx8mp-lpddr4-evk = " jailhouse"
MACHINE_FEATURES_append_imx8mp-lpddr4-evk = " optee nxp8997"
KERNEL_DEVICETREE_append_imx8mp-lpddr4-evk = " \
    freescale/imx8mp-evk-ecspi-slave.dtb \
    freescale/imx8mp-evk-usdhc1-m2.dtb \
"
WKS_FILE_DEPENDS_append_imx8mp-lpddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES_append_imx8mp-lpddr4-evk = " \
    imx8mp_m7_TCM_hello_world.bin \
    imx8mp_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mp_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mp_m7_TCM_sai_low_power_audio.bin \
"

# Overrides for imx8mp-ddr4-evk
#MACHINE_FEATURES_IMX_REMOVALS_append_imx8mp-ddr4-evk = " jailhouse"
MACHINE_FEATURES_append_imx8mp-ddr4-evk = " optee nxp8997"
WKS_FILE_DEPENDS_append_imx8mp-ddr4-evk = " imx-m7-demos"
IMAGE_BOOT_FILES_append_imx8mp-ddr4-evk = " \
    imx8mp_m7_TCM_hello_world.bin \
    imx8mp_m7_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mp_m7_TCM_rpmsg_lite_str_echo_rtos.bin \
    imx8mp_m7_TCM_sai_low_power_audio.bin \
"

# Overrides for imx8mq-evk.conf
MACHINE_FEATURES_append_imx8mq-evk = " nxp8997"
WKS_FILE_DEPENDS_append_imx8mq-evk = " imx-m4-demos"
IMAGE_BOOT_FILES_append_imx8mq-evk = " \
    imx8mq_m4_TCM_hello_world.bin \
    imx8mq_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx8mq_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
"

# Overrides for imx8qm-mek.conf
MACHINE_FEATURES_append_imx8qm-mek = " xen nxp8997"
RDEPENDS_${KERNEL_PACKAGE_NAME}-image_imx8qm-mek = ""
IMXBOOT_TARGETS_SD_prepend_imx8qm-mek = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8qxp-mek.conf
MACHINE_FEATURES_append_imx8qxp-mek = " nxp8997"
RDEPENDS_${KERNEL_PACKAGE_NAME}-image_imx8qxp-mek = ""
IMXBOOT_TARGETS_SD_prepend_imx8qxp-mek = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8dx-mek.conf
MACHINE_FEATURES_append_imx8dx-mek = " nxp8997"
IMXBOOT_TARGETS_SD_prepend_imx8dx-mek = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8dxl-lpddr4-evk.conf
MACHINE_FEATURES_append_imx8dxl-lpddr4-evk = " nxp8997"
RDEPENDS_${KERNEL_PACKAGE_NAME}-image_imx8dxl-lpddr4-evk = ""
IMXBOOT_TARGETS_SD_prepend_imx8dxl-lpddr4-evk = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4 flash_linux_m4_no_v2x ', \
                                                       'flash_regression_linux_m4', d)} "

# Overrides for imx8dxl-ddr3l-evk.conf
MACHINE_FEATURES_append_imx8dxl-ddr3l-evk = " nxp8997"
RDEPENDS_${KERNEL_PACKAGE_NAME}-image_imx8dxl-ddr3l-evk = ""
IMXBOOT_TARGETS_SD_prepend_imx8dxl-ddr3l-evk = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4 flash_linux_m4_no_v2x', \
                                                       'flash_regression_linux_m4', d)} "
