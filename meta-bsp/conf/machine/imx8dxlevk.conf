#@TYPE: Machine
#@NAME: i.MX 8DXL EVK
#@DESCRIPTION: i.MX 8DXL Evaluation Kit (EVK) board

MACHINEOVERRIDES =. "mx8:mx8x:mx8dxl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

MACHINE_FEATURES_append = " pci optee bcm43455 bcm4356 bcm4359 mrvl8997"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""
RDEPENDS_${KERNEL_PACKAGE_NAME}-image = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/imx8dxl-evk.dtb \
                     freescale/imx8dxl-evk-enet0.dtb freescale/imx8dxl-evk-enet0-tja1100.dtb \
                     freescale/imx8dxl-evk-inmate.dtb freescale/imx8dxl-evk-lpspi-slave.dtb \
                     freescale/imx8dxl-evk-pcie-ep.dtb \
                     freescale/imx8dxl-evk-root.dtb \
                     freescale/imx8dxl-evk-rpmsg.dtb"

UBOOT_MAKE_TARGET = "all"
SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx8dxl_evk_defconfig,sdcard"
UBOOT_CONFIG[fspi]   = "imx8dxl_evk_fspi_defconfig"

IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_linux_m4 flash_linux_m4_no_v2x flash_spl', \
                                                       'flash_regression_linux_m4 flash', d)}"
IMXBOOT_TARGETS_FSPI = \
    "${@bb.utils.contains('MACHINE_FEATURES', 'optee', 'flash_spl_flexspi', \
                                                       'flash_flexspi', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',        'sd', '${IMXBOOT_TARGETS_SD}', \
                                                       '${IMXBOOT_TARGETS_FSPI}', d)}"

BOARD_TYPE = "evk"
