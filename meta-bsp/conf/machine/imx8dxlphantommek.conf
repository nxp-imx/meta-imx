#@TYPE: Machine
#@NAME: i.MX 8DXL Phantom MEK
#@DESCRIPTION: i.MX 8DXL Phantom Multisensory Enablement Kit (MEK) board

MACHINEOVERRIDES =. "mx8:mx8x:mx8phantomdxl:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

MACHINE_FEATURES_append = " pci bcm43455 bcm4356 bcm4359"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""
RDEPENDS_${KERNEL_PACKAGE_NAME}-image = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyLP0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/imx8dxl-phantom-mek.dtb freescale/imx8dxl-phantom-mek-rpmsg.dtb"

UBOOT_MAKE_TARGET = "all"
SPL_BINARY = "spl/u-boot-spl.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "imx8dxl_phantom_mek_defconfig,sdcard"
UBOOT_CONFIG[fspi] = "imx8dxl_phantom_mek_fspi_defconfig"

IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',         'sd', 'flash flash_regression_linux_m4 flash_spl', \
                                                        'flash_flexspi flash_spl_flexspi', d)}"

BOARD_TYPE = "mek"
