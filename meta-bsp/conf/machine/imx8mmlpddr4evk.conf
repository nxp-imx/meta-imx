#@TYPE: Machine
#@NAME: NXP i.MX8MM LPDDR4 Evaluation Kit
#@SOC: i.MX8MM
#@DESCRIPTION: Machine configuration for NXP i.MX8MMini LPDDR4 EVK
#@MAINTAINER: Jun Zhu <junzhu@nxp.com>

MACHINEOVERRIDES =. "mx8:mx8m:mx8mm:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES += " pci wifi bluetooth optee qca9377 bcm43455 bcm4356 bcm4359"

KERNEL_DEVICETREE = "freescale/imx8mm-evk.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-ak4497.dtb freescale/imx8mm-evk-ak5558.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-audio-tdm.dtb freescale/imx8mm-evk-inmate.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-pcie-ep.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-revb.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-rm67191.dtb freescale/imx8mm-evk-root.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-rpmsg.dtb"
KERNEL_DEVICETREE += "freescale/imx8mm-evk-8mic-revE.dtb freescale/imx8mm-evk-8mic-swpdm.dtb"

WKS_FILE_DEPENDS_append = " imx-m4-demos"
IMAGE_BOOT_FILES += "imx8mm_m4_TCM_hello_world.bin \
                    imx8mm_m4_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
                    imx8mm_m4_TCM_rpmsg_lite_str_echo_rtos.bin \
                    imx8mm_m4_TCM_sai_low_power_audio.bin \
"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mm_evk_config,sdcard"
UBOOT_CONFIG[fspi] = "imx8mm_evk_fspi_defconfig"
SPL_BINARY = "spl/u-boot-spl.bin"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = "lpddr4_pmu_train_1d_imem.bin lpddr4_pmu_train_1d_dmem.bin lpddr4_pmu_train_2d_imem.bin lpddr4_pmu_train_2d_dmem.bin"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mm-evk.dtb"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "${@bb.utils.contains('UBOOT_CONFIG', 'fspi', 'flash_evk_flexspi', 'flash_evk', d)}"

SERIAL_CONSOLES = "115200;ttymxc1"

IMAGE_BOOTLOADER = "imx-boot"

LOADADDR = ""
IMX_BOOT_SEEK = "33"
