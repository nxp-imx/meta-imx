#@TYPE: Machine
#@NAME: i.MX 8QXP MEK
#@DESCRIPTION: i.MX 8QXP Multisensory Enablement Kit (MEK) board

MACHINEOVERRIDES =. "mx8:mx8qxp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/arch-arm64.inc

MACHINE_FEATURES_append = " optee qca6174 bcm43455 bcm4356 bcm4359"

# Don't include kernels in standard images
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""
RDEPENDS_${KERNEL_PACKAGE_NAME}-image = ""

LOADADDR = ""

# Disable the serial console due to auto-serial-console
SERIAL_CONSOLES = "115200;ttyAMA0"

# we do not want to have getty running on tty1 as we run
# auto-serial-console there
USE_VT = "0"

KERNEL_DEVICETREE = "freescale/fsl-imx8qxp-mek.dtb freescale/fsl-imx8qxp-mek-dom0.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-dsi-rm67191.dtb freescale/fsl-imx8qxp-mek-dsp.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-enet2.dtb freescale/fsl-imx8qxp-mek-enet2-tja1100.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-it6263-lvds0-dual-channel.dtb freescale/fsl-imx8qxp-mek-it6263-lvds1-dual-channel.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-inmate.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-lcdif.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-jdi-wuxga-lvds1-panel.dtb freescale/fsl-imx8qxp-mek-jdi-wuxga-lvds0-panel.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-ov5640.dtb"
KERNEL_DEVICETREE += "freescale/fsl-imx8qxp-mek-m4.dtb freescale/fsl-imx8qxp-mek-root.dtb"

UBOOT_MAKE_TARGET = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', '', \
                                                        'u-boot.bin', d)}"
SPL_BINARY = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'spl/u-boot-spl.bin', \
                                                        '', d)}"
UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG_SD = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'imx8qxp_mek_spl_defconfig', \
                                                        'imx8qxp_mek_defconfig', d)}"
UBOOT_CONFIG_FSPI = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'imx8qxp_mek_spl_fspi_defconfig', \
                                                        'imx8qxp_mek_fspi_defconfig', d)}"
UBOOT_CONFIG[sd]   = "${UBOOT_CONFIG_SD},sdcard"
UBOOT_CONFIG[fspi] = "${UBOOT_CONFIG_FSPI}"


BOOT_SPACE = "65536"
IMAGE_BOOTLOADER = "imx-boot"
IMX_BOOT_SEEK = "32"

IMXBOOT_TARGETS_SD = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'flash_all_spl_container flash_spl_container', \
                                                        'flash_all flash', d)}"
IMXBOOT_TARGETS_FSPI = \
    "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'flash_spl_flexspi_container', \
                                                        'flash_flexspi', d)}"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG',         'sd', '${IMXBOOT_TARGETS_SD}', \
                                                        '${IMXBOOT_TARGETS_FSPI}', d)}"

BOARD_TYPE = "mek"
