#@TYPE: Machine
#@NAME: NXP i.MX7ULP Evaluation Kit
#@SOC: i.MX7ULP
#@DESCRIPTION: Machine configuration for NXP i.MX7ULP EVK
#@MAINTAINER: Lauren Post <Lauren.Post@nxp.com>

MACHINEOVERRIDES =. "mx7:mx7ulp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa7.inc

MACHINE_FEATURES += " pci wifi bluetooth optee"

KERNEL_DEVICETREE = "imx7ulp-evk.dtb imx7ulp-evk-emmc.dtb imx7ulp-evk-emmc-qspi.dtb imx7ulp-evk-ft5416.dtb imx7ulp-evk-mipi.dtb"
KERNEL_DEVICETREE += "imx7ulp-evk-lpuart.dtb imx7ulp-evk-qspi.dtb imx7ulp-evk-sd1.dtb imx7ulp-evk-sensors-to-i2c5.dtb"
KERNEL_DEVICETREE += "imx7ulp-evk-wm8960.dtb"
## imx7ulp doesn't have a dtb file for optee

IMAGE_BOOTFILES_DEPENDS += "imx-m4-demos:do_deploy"
IMAGE_BOOTFILES += "imx7ulp_m4_demo.img imx7ulp_wireless_uart_bridge.img imx7ulp_erpc_matrix_multiply_rpmsg_rtos_imxcm4.img imx7ulp_rpmsg_lite_pingpong_rtos.img imx7ulp_rpmsg_lite_str_echo_rtos.img"

UBOOT_CONFIG ??= "${@bb.utils.contains('COMBINED_FEATURES', 'optee', 'sd-optee', 'sd', d)}"
UBOOT_CONFIG[sd] = "mx7ulp_evk_config,sdcard"
UBOOT_CONFIG[emmc] = "mx7ulp_evk_emmc_config,sdcard"
UBOOT_CONFIG[mfgtool] = "mx7ulp_evk_config"
UBOOT_CONFIG[sd-optee] = "mx7ulp_evk_optee_config,sdcard"

OPTEE_BIN_EXT = "7ulp"

MACHINE_SOCARCH_SUFFIX = "-mx7ulp"
MACHINE_SOCARCH_FILTER_append_mx7ulp = "virtual/libopenvg virtual/libgles1 virtual/libgles2 virtual/egl virtual/mesa virtual/libgl virtual/libg2d"
IMX_DEFAULT_KERNEL_mx7ulp = "linux-imx"

# Install WiFi firmware
MACHINE_FIRMWARE_append = " firmware-imx-brcm firmware-qca9377"

# Add kernel modules
MACHINE_EXTRA_RRECOMMENDS += "\
    kernel-module-qcacld \
"


# Set Serial console
SERIAL_CONSOLE = "115200 ttyLP0"
