#@TYPE: Machine
#@NAME: NXP i.MX7D SABRE Smart Device
#@SOC: i.MX7D
#@DESCRIPTION: Machine configuration for NXP i.MX7D SABRE Smart Device
#@MAINTAINER: Lauren Post <Lauren.Post@nxp.com>

MACHINEOVERRIDES =. "mx7:mx7d:"

require conf/machine/include/imx-base.inc
require conf/machine/include/tune-cortexa7.inc

MACHINE_FEATURES += " pci wifi bluetooth"

KERNEL_DEVICETREE = "imx7d-sdb.dtb imx7d-sdb-epdc.dtb imx7d-sdb-gpmi-weim.dtb \
                     imx7d-sdb-m4.dtb imx7d-sdb-mipi-dsi.dtb imx7d-sdb-qspi.dtb \
                     imx7d-sdb-reva.dtb imx7d-sdb-reva-epdc.dtb imx7d-sdb-reva-gpmi-weim.dtb \
                     imx7d-sdb-reva-hdmi-audio.dtb imx7d-sdb-reva-m4.dtb imx7d-sdb-reva-qspi.dtb \
                     imx7d-sdb-reva-touch.dtb imx7d-sdb-reva-wm8960.dtb"

IMAGE_M4LOADER = "imx-m4-demos"
IMAGE_M4 = "imx7d_sabresd_m4_TCM_Pingpang.bin imx7d_sabresd_m4_TCM_helloworld.bin imx7d_sabresd_m4_TCM_mcctty.bin"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "mx7dsabresd_config,sdcard"
UBOOT_CONFIG[qspi1] = "mx7dsabresd_qspi1_config"
UBOOT_CONFIG[nand] = "mx7dsabresd_nand_config,ubifs"
UBOOT_CONFIG[epdc] = "mx7dsabresd_epdc_config"
UBOOT_CONFIG[nonsec] = "mx7dsabresd_nonsec_config,sdcard"
UBOOT_CONFIG[mfgtool] = "mx7dsabresd_config"

XSERVER += "mesa-driver-swrast"

# Install brcm firmware
MACHINE_FIRMWARE_append = " firmware-imx-brcm"

BOOT_SPACE_WITHOUT_OPTEE = "8192"
BOOT_SPACE_WITH_OPTEE = "10240"
BOOT_SPACE = '${@bb.utils.contains("COMBINED_FEATURES", "optee", ${BOOT_SPACE_WITH_OPTEE}, ${BOOT_SPACE_WITHOUT_OPTEE}, d)}'
